* --BaseName=10x10NEW --niter=1 --ifCSV=0 -idir=Data
* --BaseName=G-cubed  --niter=1 --ifCSV=0 -idir=Data

*---    [EditJean]: paths for various directory
$include "%system.fp%..\a_FolderPath.gms"
$IF NOT SET BaseName abort "$SetGlobal BaseName should be defined" ;
$IF NOT SET DataDir  $SetGlobal DataDir "."
$IF NOT SET Prefix       $SetGlobal Prefix ""
$If     SET oDirCheck $SetGlobal DirCheck "%oDirCheck%\%BaseName%\agg"
$If NOT SET oDirCheck $SetGlobal DirCheck "%BaseName%\Check\agg"

$IF NOT DEXIST "%iDataDir%\Alt" $call "mkdir %iDataDir%\Alt"

*$macro m_PUTYEAR years(t):4:0
$macro m_PUTYEAR t.tl
$setGlobal SimName "AlterTax"
$setGlobal wDir %iDataDir%\Flt
$setGlobal oDir %iDataDir%\Alt

* [EditJean]: if no filtering procedure has been run then take infos from \Agg
$IF NOT EXIST %iDataDir%\Flt\dat.gdx $setGlobal wDir %iDataDir%\Agg

sets
   tt       "Time framework"       / base, check, shock /
   t(tt)    "Model time framework" / base, check, shock /
   t0(t)    "Initial year"         / base /
   ts(t)    "Simulation years"
;

alias(tsim,t) ;

parameters
   year0       "Base year (in value)"
   years(tt)   "Vector of years in value"
   gap(tt)     "Gap between model years"
   IfSub       "Set to 1 to substitute equations" / 1 /
   ifMCP       "Set to 1 to solve using MCP"      / 1 /
   xpScale     "Scale factor for output"          / 1 /
   niter(t)    "Number of iterations for solve"
   iter        "Iteration counter"
;

years(tt) = ord(tt) + 0 ;
loop(t0, year0 = years(t0) ; ) ;

niter(t) = 1 ;
niter(t) $ sameas("shock",t) = %niter% ;

file screen / con / ;
put screen ;

*---    Load sets for the model automatically generated by "AggGTAP.gms"
* [EditJean]: change set file
*$include "%iDataDir%\Alt\%Prefix%Sets.gms"$include "%iDataDir%\Alt\%Prefix%Sets.gms"
$include "%DataDir%\AlterTax\AlterTaxPrm.gms"

$include "%DataDir%\GTAPModel\model.gms"

* ------------------------------------------------------------------------------
*
*  OVERRIDE GTAP ELASTICITIES
*
* ------------------------------------------------------------------------------

work = 1.06 ;

sigmap(r,a)    = work ;
sigmav(r,a)    = work ;
sigmand(r,a)   = work ;
sigmai(r)      = work ;
sigmam(r,i,aa) = work ;
sigmaw(r,i)    = work ;
RoRFlag        = capFix ;

*  >>>>> Utility is set to CD in *Prm.gms file

$include "%DataDir%\GTAPModel\cal.gms"

options limrow = 3, limcol = 3, solprint=off ;
*options iterlim = 100 ;
gtap.scaleopt   = 1 ;
gtap.tolinfrep  = 1e-5 ;

loop(tsim,
    $$include "%DataDir%\GTAPModel\iterloop.gms"
    rs(r)    = yes ;
    ts(t)    = no ;
    ts(tsim) = yes ;

*------------------------------------------------------------------------------*
*                           IMPOSE SHOCK(S)                                    *
*------------------------------------------------------------------------------*
    IF(sameas(tsim,"shock"),
        $$IF EXIST "%iFilesDir%\%prefix%Alt.gms" $include "%iFilesDir%\%prefix%Alt.gms"
    );

    IF(years(tsim) gt year0,
        $$batinclude "%DataDir%\GTAPModel\solve.gms" gtap
    );

    put screen ;
    put / ;
    put "Walras: ", (walras.l/gblScale):15:6 / ;
    putclose screen ;
);

file csv / %DirCheck%\Alt.csv / ;

IF(%ifCSV%,
    put csv ;
    put "Variable,Region,Sector,Qualifier,Year,Value" / ;
    csv.pc=5 ;
    csv.nd=9 ;
    $$include "%DataDir%\GTAPModel\postsim.gms"
);

$batinclude "%DataDir%\GTAPModel\saveData" shock

*---    [EditJean]:
*Execute_Unload "%oDir%\AlterTax.gdx" ;
execute_unload "%DirCheck%\%GTAP_DBType%V%GTAP_ver%Y%YearGTAP%_AlterTax.gdx"

*---    [EditJean]: store info about AlterTax operation
$Batinclude "%ToolsDir%\StoreInTxtFileProjectDetails.gms" "altertax"


